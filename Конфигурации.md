Содержание
==========
* [Terraform](#Terraform)
* [Ansible](#Ansible)
   
---------
## Terraform

`/home/admin/terraform/`

<details>

*<summary>meta.txt</summary>*

``` GO

#cloud-config
users:
  - name: admin
    groups: sudo
    shell: /bin/bash
    sudo: ['ALL=(ALL) NOPASSWD:ALL']
    ssh-authorized-keys:
            - ssh-rsa AAAAB3NzaC1yc2EAAAADAQABAAABgQC3psGSU2mki9Pj/deYEU8soWQfrCz8mb9xNLJsoxjw5XflrOgxZyIQ15hy7obnLT6wj9DzJ6TWhiWHfYX1e6dCSaTWaI9ACa1CtJT5w2eS3fj+pi1THrTIXQnrz3VXX6hzGBRcHoTF2J34t6dcJU0g8JXffZjMQftA8IZK+fAXUNoaqrNKm67mQ5KtlzXazpZ6ZBceUZLsQqaQRQbJODJkcgjih3jN+fg89wCfL/zi7iN83EWgnVnRe1Ks4elGOhbyk74jJAOpw4+/yekYTdrxXMb4K4P5rbnYlCTIWmkFelEz4mAf9Ym1bILicvPOt+yRLp0TW+bf/ewQRMc7XlkpUOteuAQAwlkPmCJcD5cjzqbLukSq3PvTodcdYrUu4NTjZ475HqKhq61s0CCjvEQ/nCDdeNnmASopJLLo+9jE/kqUzoeczE5nGSgSQQowa9NjSDhvDw0PhEdRCnZY4DFvJDSHGmpfPLooqcqhsQe2zXLM7EOCCBX3e9O41lYpoSU= admin@terraform
```
</details>

<details>

*<summary>main.tf</summary>*

``` GO

terraform {
  required_providers {
    yandex = {
      source = "yandex-cloud/yandex"
    }
  }
  required_version = ">= 0.13"
}

provider "yandex" {
  cloud_id  = "b1gaipai88l7kddo0fdf"
  folder_id = "b1gfa8q3vq144o92ul90"
  zone      = "ru-central1-a"
}

resource "yandex_vpc_network" "porvatov" {
  name = "porvatov-network"
}

resource "yandex_vpc_subnet" "inner-web-1" {
  name           = "web-1-subnet"
  zone           = "ru-central1-a"
  network_id     = yandex_vpc_network.porvatov.id
  v4_cidr_blocks = ["10.0.1.0/28"]
  route_table_id = yandex_vpc_route_table.inner-to-nat.id 
}

resource "yandex_vpc_subnet" "inner-web-2" {
  name           = "web-2-subnet"
  zone           = "ru-central1-b"
  network_id     = yandex_vpc_network.porvatov.id
  v4_cidr_blocks = ["10.0.2.0/28"]
  route_table_id = yandex_vpc_route_table.inner-to-nat.id
}

resource "yandex_vpc_subnet" "inner-services" {
  name           = "inner-services-subnet"
  zone           = "ru-central1-c"
  network_id     = yandex_vpc_network.porvatov.id
  v4_cidr_blocks = ["10.0.3.0/27"]
  route_table_id = yandex_vpc_route_table.inner-to-nat.id
}

resource "yandex_vpc_subnet" "public" {
  name           = "public-subnet"
  zone           = "ru-central1-c"
  network_id     = yandex_vpc_network.porvatov.id
  v4_cidr_blocks = ["10.0.10.0/27"]
}

resource "yandex_compute_instance" "web-1" {
  name        = "vm-web-1"
  hostname    = "web-1"
  platform_id = "standard-v3"
  allow_stopping_for_update = true
  zone        = "ru-central1-a"

  resources {
    core_fraction = 20
    cores  = 2
    memory = 1
  }

  boot_disk {
    initialize_params {
      image_id = "fd843htdp8usqsiji0bb"
    }
  }

  network_interface {
    subnet_id          = yandex_vpc_subnet.inner-web-1.id
    security_group_ids = [yandex_vpc_security_group.inner.id]
    ip_address         = "10.0.1.3"
  }

  metadata = {
    user-data = "${file("./meta.txt")}"
  }
}

resource "yandex_compute_instance" "web-2" {
  name        = "vm-web-2"
  hostname    = "web-2"
  platform_id = "standard-v3"
  allow_stopping_for_update = true
  zone        = "ru-central1-b"

  resources {
    core_fraction = 20
    cores  = 2
    memory = 1
  }

  boot_disk {
    initialize_params {
      image_id = "fd843htdp8usqsiji0bb"
    }
  }

  network_interface {
    subnet_id          = yandex_vpc_subnet.inner-web-2.id
    security_group_ids = [yandex_vpc_security_group.inner.id]
    ip_address         = "10.0.2.3"
  }

  metadata = {
    user-data = "${file("./meta.txt")}"
  }
}

resource "yandex_alb_target_group" "web" {
  name = "web-target-group"

  target {
    ip_address = yandex_compute_instance.web-1.network_interface.0.ip_address
    subnet_id  = yandex_vpc_subnet.inner-web-1.id
  }

  target {
    ip_address = yandex_compute_instance.web-2.network_interface.0.ip_address
    subnet_id  = yandex_vpc_subnet.inner-web-2.id
  }
}

resource "yandex_alb_backend_group" "web" {
  name = "web-backend-group"

  http_backend {
    name             = "http-backend"
    weight           = 1
    port             = 80
    target_group_ids = [yandex_alb_target_group.web.id]
    load_balancing_config {
      panic_threshold = 90
    }
    healthcheck {
      timeout             = "10s"
      interval            = "2s"
      healthy_threshold   = 10
      unhealthy_threshold = 15
      http_healthcheck {
        path = "/"
      }
    }
  }
}

resource "yandex_alb_http_router" "web" {
  name = "web-http-router"
}

resource "yandex_alb_virtual_host" "root" {
  name           = "root-virtual-host"
  http_router_id = yandex_alb_http_router.web.id
  route {
    name = "root-path"
    http_route {
      http_match {
        path {
          prefix = "/"
        }
      }
      http_route_action {
        backend_group_id = yandex_alb_backend_group.web.id
        timeout          = "3s"
      }
    }
  }
}

resource "yandex_alb_load_balancer" "web" {
  name               = "web-load-balancer"
  network_id         = yandex_vpc_network.porvatov.id
  security_group_ids = [yandex_vpc_security_group.public-load-balancer.id, yandex_vpc_security_group.inner.id]

  allocation_policy {
    location {
      zone_id   = "ru-central1-c"
      subnet_id = yandex_vpc_subnet.inner-services.id
    }
  }

  listener {
    name = "listener1"
    endpoint {
      address {
        external_ipv4_address {
        }
      }
      ports = [80]
    }
    http {
      handler {
        http_router_id = yandex_alb_http_router.web.id
      }
    }
  }
}

resource "yandex_vpc_security_group" "inner" {
  name       = "inner-rules"
  network_id = yandex_vpc_network.porvatov.id

  ingress {
    protocol       = "ANY"
    description    = "allow any connection from inner subnets"
    v4_cidr_blocks = ["10.0.1.0/28", "10.0.2.0/28", "10.0.3.0/27", "10.0.10.0/27"]
  }

  egress {
    protocol       = "ANY"
    description    = "allow any outgoing connections"
    v4_cidr_blocks = ["0.0.0.0/0"]
  }
}

resource "yandex_vpc_security_group" "public-load-balancer" {
  name       = "public-load-balancer-rules"
  network_id = yandex_vpc_network.porvatov.id

  ingress {
    protocol          = "ANY"
    description       = "Health checks"
    #port              = 80
    v4_cidr_blocks    = ["198.18.235.0/24", "198.18.248.0/24"]
    predefined_target = "loadbalancer_healthchecks"
  }

  ingress {
    protocol       = "TCP"
    description    = "allow HTTP connections from internet"
    v4_cidr_blocks = ["0.0.0.0/0"]
    port           = 80
  }

  ingress {
    protocol       = "ICMP"
    description    = "allow ping"
    v4_cidr_blocks = ["0.0.0.0/0"]
  }

  egress {
    protocol       = "ANY"
    description    = "allow any outgoing connection"
    v4_cidr_blocks = ["0.0.0.0/0"]
  }
}

output "external_ip_addres_load_balancer" {
  value = yandex_alb_load_balancer.web.listener.0.endpoint.0.address.0.external_ipv4_address
}

resource "yandex_vpc_route_table" "inner-to-nat" {
  network_id = yandex_vpc_network.porvatov.id

  static_route {
    destination_prefix = "0.0.0.0/0"
    next_hop_address   = yandex_compute_instance.bastion.network_interface.0.ip_address
  }
}

resource "yandex_compute_instance" "bastion" {
  name        = "vm-bastion"
  hostname    = "bastion"
  platform_id = "standard-v3"
  zone        = "ru-central1-c"

  resources {
    cores  = 2
    memory = 2
  }

  boot_disk {
    initialize_params {
      image_id = "fd82v0f4ufbnvm3b9s08" # nat-instance-ubuntu-18-04-lts-v20220520
    }
  }

  network_interface {
    subnet_id          = yandex_vpc_subnet.public.id
    nat                = true
    security_group_ids = [yandex_vpc_security_group.inner.id, yandex_vpc_security_group.public-bastion.id]
    ip_address         = "10.0.10.5"
  }

  metadata = {
    user-data = "${file("./meta.txt")}"
  }
}

resource "yandex_vpc_security_group" "public-bastion" {
  name       = "public-bastion-rules"
  network_id = yandex_vpc_network.porvatov.id

  ingress {
    protocol       = "TCP"
    description    = "allow ssh connections from internet"
    v4_cidr_blocks = ["0.0.0.0/0"]
    port           = 22
  }

  ingress {
    protocol       = "ICMP"
    description    = "allow ping"
    v4_cidr_blocks = ["0.0.0.0/0"]
  }

  egress {
    protocol       = "ANY"
    description    = "allow any outgoing connection"
    v4_cidr_blocks = ["0.0.0.0/0"]
  }
}

resource "yandex_compute_instance" "prometheus" {
  name        = "vm-prometheus"
  hostname    = "prometheus"
  platform_id = "standard-v3"
  zone        = "ru-central1-c"

  resources {
    cores  = 2
    memory = 2
  }

  boot_disk {
    initialize_params {
      image_id = "fd85jf9kn9r40o1neolo" # debian-11-v20220509
    }
  }

  network_interface {
    subnet_id          = yandex_vpc_subnet.inner-services.id
    security_group_ids = [yandex_vpc_security_group.inner.id]
    ip_address         = "10.0.3.10"
  }

  metadata = {
    user-data = "${file("./meta.txt")}"
  }
}

resource "yandex_compute_instance" "grafana" {
  name        = "vm-grafana"
  hostname    = "grafana"
  platform_id = "standard-v3"
  zone        = "ru-central1-c"

  resources {
    cores  = 2
    memory = 2
  }

  boot_disk {
    initialize_params {
      image_id = "fd85jf9kn9r40o1neolo" # debian-11-v20220509
    }
  }

  network_interface {
    subnet_id          = yandex_vpc_subnet.public.id
    nat                = true
    security_group_ids = [yandex_vpc_security_group.inner.id, yandex_vpc_security_group.public-grafana.id]
    ip_address         = "10.0.10.11"
  }

  metadata = {
    user-data = "${file("./meta.txt")}"
  }
}

resource "yandex_vpc_security_group" "public-grafana" {
  name       = "public-grafana-rules"
  network_id = yandex_vpc_network.porvatov.id

  ingress {
    protocol       = "TCP"
    description    = "allow grafana connections from internet"
    v4_cidr_blocks = ["0.0.0.0/0"]
    port           = 3000
  }

  ingress {
    protocol       = "ICMP"
    description    = "allow ping"
    v4_cidr_blocks = ["0.0.0.0/0"]
  }

  egress {
    protocol       = "ANY"
    description    = "allow any outgoing connection"
    v4_cidr_blocks = ["0.0.0.0/0"]
  }
}

resource "yandex_compute_instance" "elastic" {
  name        = "vm-elastic"
  hostname    = "elastic"
  platform_id = "standard-v3"
  zone        = "ru-central1-c"

  resources {
    cores  = 2
    memory = 2
  }

  boot_disk {
    initialize_params {
      image_id = "fd85jf9kn9r40o1neolo" # debian-11-v20220509
      size     = 6
    }
  }

  network_interface {
    subnet_id          = yandex_vpc_subnet.inner-services.id
    security_group_ids = [yandex_vpc_security_group.inner.id]
    ip_address         = "10.0.3.12"
}

  metadata = {
    user-data = "${file("./meta.txt")}"
  }
}

resource "yandex_compute_instance" "kibana" {
  name        = "vm-kibana"
  hostname    = "kibana"
  platform_id = "standard-v3"
  zone        = "ru-central1-c"

  resources {
    cores  = 2
    memory = 2
  }

  boot_disk {
    initialize_params {
      image_id = "fd85jf9kn9r40o1neolo" # debian-11-v20220509
    }
  }

  network_interface {
    subnet_id          = yandex_vpc_subnet.public.id
    nat                = true
    security_group_ids = [yandex_vpc_security_group.inner.id, yandex_vpc_security_group.public-kibana.id]
    ip_address         = "10.0.10.13"
  }

  metadata = {
    user-data = "${file("./meta.txt")}"
  }
}

resource "yandex_vpc_security_group" "public-kibana" {
  name       = "public-kibana-rules"
  network_id = yandex_vpc_network.porvatov.id

  ingress {
    protocol       = "TCP"
    description    = "allow kibana connections from internet"
    v4_cidr_blocks = ["0.0.0.0/0"]
    port           = 5601
  }

  ingress {
    protocol       = "ICMP"
    description    = "allow ping"
    v4_cidr_blocks = ["0.0.0.0/0"]
  }

  egress {
    protocol       = "ANY"
    description    = "allow any outgoing connection"
    v4_cidr_blocks = ["0.0.0.0/0"]
  }
}

output "external_ip_addres_bastion-ssh" {
  value = yandex_compute_instance.bastion.network_interface.0.nat_ip_address
}

resource "yandex_compute_snapshot_schedule" "default" {
  name = "snap"

  schedule_policy {
    expression = "00 23 ? * *"
  }

  snapshot_count = 7

  disk_ids = [yandex_compute_instance.web-1.boot_disk[0].disk_id,
              yandex_compute_instance.web-2.boot_disk[0].disk_id,
              yandex_compute_instance.bastion.boot_disk[0].disk_id,
              yandex_compute_instance.prometheus.boot_disk[0].disk_id,
              yandex_compute_instance.grafana.boot_disk[0].disk_id, 
              yandex_compute_instance.elastic.boot_disk[0].disk_id,           
              yandex_compute_instance.kibana.boot_disk[0].disk_id,
             ]

}
```
</details>

## Ansible

`/etc/ansible/`

<details>

*<summary>ansible.cfg</summary>*

``` GO

[defaults]
inventory = ./hosts
remote_user = admin
roles_path = ./roles

[privilege_escalation]
become = True
become_user = root
become_method = sudo
```
</details>

<details>

*<summary>hosts</summary>*

``` GO

[bastion]
10.0.10.5 

[public-balancer]
84.252.131.55

[web]
10.0.1.3
10.0.2.3

[prometheus]
10.0.3.10

[grafana]
10.0.10.11 

[elastic]
10.0.3.12

[kibana]
10.0.10.13 
```
</details>

#### Конфигурации `Ansible` с плейбуками представлены в разделе [Playbooks](https://github.com/IlyaPorvatov/Diplom/tree/main/Playbooks)

#### Конфигурации `Ansible` с ролями представлены в разделе [Roles](https://github.com/IlyaPorvatov/Diplom/tree/main/Roles)